       IDENTIFICATION DIVISION.
       PROGRAM-ID. LODCOMPR.
       AUTHOR. R.STARNES.
       DATE-WRITTEN. JUNE 2, 1988.
      *    Lists members of 1, 2, or 3 load module library directories
      *    (PDS or PDSE), showing linkedit/bind dates, sizes, EPA's,
      *    and aliases, and flagging any differences.
      *    All input file I/O is done in subprogram LODCMPIO, which
      *    is in Assembler.
      *    Execution JCL:
      *    //S1 EXEC PGM=LODCOMPR,PARM='FILES=n,LIST=value'
      *          FILES is required and must be either 1, 2, or 3.
      *          LIST=ALL:  All members of all files are listed.
      *             Exceptions (members that don't match in
      *             size/EPA/date/alias, or that are not present
      *             in all files) are asterisked.
      *          LIST=EXCEPTIONS:  Only exceptions are listed;
      *             members present in all files with identical
      *             size/EPA/date/alias are omitted.
      *          LIST=DUPS:  All members present in more than one
      *             file are listed (regardless of whether their
      *             size/EPA/date/alias match).  Members present
      *             in exactly one file are omitted.
      *          If LIST is not coded, LIST=EXCEPTIONS is default.
      *          If FILES=1, LIST=ALL is forced.
      *    //INPUT1 DD DSN=...
      *    //INPUT2 DD DSN=...   (may be omitted if FILES=1)
      *    //INPUT3 DD DSN=...   (may be omitted if FILES=1 or 2)
      *    //SYSPRINT DD SYSOUT=*,DCB=RECFM=FA
      *
      *   Modified 3/2/2021 to go to four-digit years; additional
      *   changes in LODCMPIO.
      *
       ENVIRONMENT DIVISION.
       CONFIGURATION SECTION.
       SOURCE-COMPUTER. IBM-370.
       OBJECT-COMPUTER. IBM-370.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT PRTFILE   ASSIGN TO UT-S-SYSPRINT.
       DATA DIVISION.
       FILE SECTION.
       FD  PRTFILE
           RECORDING MODE F  LABEL RECORDS OMITTED
           BLOCK CONTAINS 1 RECORDS  RECORD CONTAINS 133 CHARACTERS
           DATA RECORD IS PRT-REC.
       01  PRT-REC.
           05  CARR-CTL  PIC X.
           05  FILLER  PIC X(132).
       WORKING-STORAGE SECTION.
       01  LITTLE-STUFF.
           05  FILE-1           PIC X  VALUE '1'.
           05  FILE-2           PIC X  VALUE '2'.
           05  FILE-3           PIC X  VALUE '3'.
           05  OPEN-ACTION      PIC X(5)  VALUE 'OPEN '.
           05  READ-ACTION      PIC X(5)  VALUE 'READ '.
           05  CLOSE-ACTION     PIC X(5)  VALUE 'CLOSE'.
           05  FILECOUNT        PIC X.
           05  PARM-OPTION      PIC X  VALUE 'E'.
           05  HIT-SWITCH       PIC X  VALUE 'N'.
           05  CURRENT-DATE.
               10  CURRENT-YR   PIC X(4).
               10  CURRENT-MO   PIC XX.
               10  CURRENT-DA   PIC XX.
           05  CURRENT-TIME.
               10  CURRENT-HR   PIC XX.
               10  CURRENT-MIN  PIC XX.
               10  CURRENT-SEC  PIC XX.
               10  FILLER       PIC XX.
           05  PAGE-CTR         PIC S9(4)  COMP-3  VALUE +0.
           05  LINE-CTR         PIC S99  COMP-3  VALUE +66.
       01  I-O-AREA-1.
           05  INDSNAME1  PIC X(44).
           05  INVOLSER1  PIC X(6).
           05  INNAME1    PIC X(8).
           05  INSIZE1    PIC X(6).
           05  INEPA1     PIC X(6).
           05  INDATE1    PIC X(8).
           05  INALIAS1   PIC X(8).
       01  I-O-AREA-2.
           05  INDSNAME2  PIC X(44).
           05  INVOLSER2  PIC X(6).
           05  INNAME2    PIC X(8).
           05  INSIZE2    PIC X(6).
           05  INEPA2     PIC X(6).
           05  INDATE2    PIC X(8).
           05  INALIAS2   PIC X(8).
       01  I-O-AREA-3.
           05  INDSNAME3  PIC X(44)  VALUE SPACES.
           05  INVOLSER3  PIC X(6)  VALUE SPACES.
           05  INNAME3    PIC X(8)  VALUE SPACES.
           05  INSIZE3    PIC X(6)  VALUE SPACES.
           05  INEPA3     PIC X(6)  VALUE SPACES.
           05  INDATE3    PIC X(8)  VALUE SPACES.
           05  INALIAS3   PIC X(8)  VALUE SPACES.
       01  HEADING-1.
           05  FILLER      PIC X  VALUE '1'.
           05  HD1-MO      PIC XX.
           05  FILLER      PIC X  VALUE '/'.
           05  HD1-DA      PIC XX.
           05  FILLER      PIC X  VALUE '/'.
           05  HD1-YR      PIC X(4).
           05  FILLER      PIC XX  VALUE SPACES.
           05  HD1-HR      PIC XX.
           05  FILLER      PIC X  VALUE ':'.
           05  HD1-MIN     PIC XX.
           05  FILLER      PIC X  VALUE ':'.
           05  HD1-SEC     PIC XX.
           05  FILLER      PIC X(29)  VALUE SPACES.
           05  FILLER      PIC X(43)  VALUE
                   'LOAD LIBRARY DIRECTORY LIST/COMPARE UTILITY'.
           05  FILLER      PIC X(31)  VALUE SPACES.
           05  FILLER      PIC X(5)  VALUE 'PAGE '.
           05  HD1-PAGENO  PIC ZZZ9.
       01  HEADING-2.
           05  FILLER       PIC X  VALUE '0'.
           05  FILLER       PIC X(15)  VALUE SPACES.
      *  Note that all three DSNAMEs are truncated (first 35 chars
      *  only) due to space limitations of the heading line.
           05  HD2-DSNAME1  PIC X(35).
           05  FILLER       PIC X(6)  VALUE SPACES.
           05  HD2-DSNAME2  PIC X(35).
           05  FILLER       PIC X(6)  VALUE SPACES.
           05  HD2-DSNAME3  PIC X(35).
       01  HEADING-3.
           05  FILLER       PIC X(18)  VALUE SPACES.
           05  HD3-VOLSER1  PIC X(6)  VALUE SPACES.
           05  FILLER       PIC X(35)  VALUE SPACES.
           05  HD3-VOLSER2  PIC X(6)  VALUE SPACES.
           05  FILLER       PIC X(35)  VALUE SPACES.
           05  HD3-VOLSER3  PIC X(6)  VALUE SPACES.
           05  FILLER       PIC X(27)  VALUE SPACES.
       01  HEADING-4.
           05  FILLER     PIC X  VALUE '0'.
           05  FILLER     PIC X(18)  VALUE '  MEMBER          '.
           05  HD4-DATA1  PIC X(32)  VALUE
                   'DATE     SIZE    EPA    ALIAS OF'.
           05  FILLER     PIC X(9)  VALUE SPACES.
           05  HD4-DATA2  PIC X(32)  VALUE SPACES.
           05  FILLER     PIC X(9)  VALUE SPACES.
           05  HD4-DATA3  PIC X(32)  VALUE SPACES.
       01  DETAIL-LINE.
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-NAME-ASTER    PIC X  VALUE SPACES.
           05  DET-NAME          PIC X(8).
           05  FILLER            PIC X(6)  VALUE SPACES.
           05  DET-DATE1-ASTER   PIC X  VALUE SPACES.
           05  DET-DATE1         PIC X(8).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-SIZE1-ASTER   PIC X  VALUE SPACES.
           05  DET-SIZE1         PIC X(6).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-EPA1-ASTER    PIC X  VALUE SPACES.
           05  DET-EPA1          PIC X(6).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-ALIAS1-ASTER  PIC X  VALUE SPACES.
           05  DET-ALIAS1        PIC X(8).
           05  FILLER            PIC X(6)  VALUE SPACES.
           05  DET-DATE2-ASTER   PIC X  VALUE SPACES.
           05  DET-DATE2         PIC X(8).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-SIZE2-ASTER   PIC X  VALUE SPACES.
           05  DET-SIZE2         PIC X(6).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-EPA2-ASTER    PIC X  VALUE SPACES.
           05  DET-EPA2          PIC X(6).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-ALIAS2-ASTER  PIC X  VALUE SPACES.
           05  DET-ALIAS2        PIC X(8).
           05  FILLER            PIC X(6)  VALUE SPACES.
           05  DET-DATE3-ASTER   PIC X  VALUE SPACES.
           05  DET-DATE3         PIC X(8).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-SIZE3-ASTER   PIC X  VALUE SPACES.
           05  DET-SIZE3         PIC X(6).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-EPA3-ASTER    PIC X  VALUE SPACES.
           05  DET-EPA3          PIC X(6).
           05  FILLER            PIC X  VALUE SPACES.
           05  DET-ALIAS3-ASTER  PIC X  VALUE SPACES.
           05  DET-ALIAS3        PIC X(8).
       01  ALL-MATCH-LINE  PIC X(133)  VALUE '0EVERYTHING MATCHED'.
       01  BADPARM-LINE    PIC X(133)  VALUE
               '0INVALID PARM ON EXEC STATEMENT'.
       LINKAGE SECTION.
       01  INPUT-PARM.
           05  PARM-LENGTH     PIC S9(4)  COMP.
           05  PARM-FILES      PIC X(6).
           05  PARM-FILECOUNT  PIC X.
           05  FILLER          PIC X.
           05  PARM-LIST       PIC X(6).
           05  FILLER          PIC X(9).
       PROCEDURE DIVISION USING INPUT-PARM.
       ANFANG.
           ACCEPT CURRENT-DATE FROM DATE YYYYMMDD.
           ACCEPT CURRENT-TIME FROM TIME.
           MOVE CURRENT-MO TO HD1-MO.
           MOVE CURRENT-DA TO HD1-DA.
           MOVE CURRENT-YR TO HD1-YR.
           MOVE CURRENT-HR TO HD1-HR.
           MOVE CURRENT-MIN TO HD1-MIN.
           MOVE CURRENT-SEC TO HD1-SEC.
           OPEN OUTPUT PRTFILE.
           IF PARM-LENGTH < 7 GO TO BAD-PARM.
           IF PARM-FILES NOT = 'FILES=' GO TO BAD-PARM.
           IF PARM-FILECOUNT = '1' OR '2' OR '3' NEXT SENTENCE
               ELSE GO TO BAD-PARM.
           MOVE PARM-FILECOUNT TO FILECOUNT.
           IF FILECOUNT = '1' OR PARM-LIST = 'LIST=A'
      *      If FILES=1, then force LIST=ALL
               MOVE 'A' TO PARM-OPTION
               GO TO OPEN-FILES.
           IF PARM-LENGTH = 7 OR PARM-LIST = 'LIST=E'
      *      No LIST option coded--default to LIST=EXCEPTIONS
               MOVE 'E' TO PARM-OPTION
               GO TO OPEN-FILES.
           IF PARM-LIST = 'LIST=D'
               MOVE 'D' TO PARM-OPTION
               GO TO OPEN-FILES.
           GO TO BAD-PARM.
       OPEN-FILES.
           MOVE SPACES TO HD2-DSNAME2, HD2-DSNAME3.
           CALL 'LODCMPIO' USING FILE-1, OPEN-ACTION, I-O-AREA-1.
           MOVE INDSNAME1 TO HD2-DSNAME1.
           MOVE INVOLSER1 TO HD3-VOLSER1.
           IF FILECOUNT = '1' GO TO READ-ALL-3.
           CALL 'LODCMPIO' USING FILE-2, OPEN-ACTION, I-O-AREA-2.
           MOVE INDSNAME2 TO HD2-DSNAME2.
           MOVE INVOLSER2 TO HD3-VOLSER2.
           MOVE HD4-DATA1 TO HD4-DATA2.
           IF FILECOUNT = '2' GO TO READ-ALL-3.
           CALL 'LODCMPIO' USING FILE-3, OPEN-ACTION, I-O-AREA-3.
           MOVE INDSNAME3 TO HD2-DSNAME3.
           MOVE INVOLSER3 TO HD3-VOLSER3.
           MOVE HD4-DATA1 TO HD4-DATA3.
           GO TO READ-ALL-3.
       COMPARE.
           IF FILECOUNT = '1' GO TO ALL-NAMES-MATCH.
           IF INNAME1 < INNAME2 GO TO NAME1-LOWER-THAN-NAME2.
           IF INNAME1 > INNAME2 GO TO NAME2-LOWER-THAN-NAME1.
      *  Match on names 1-2
           IF FILECOUNT = '2' GO TO ALL-NAMES-MATCH.
           IF INNAME1 < INNAME3 GO TO PRINT-NAME1-AND-NAME2.
           IF INNAME1 > INNAME3 GO TO PRINT-NAME3.
       ALL-NAMES-MATCH.
      *  Match on all 3 (or 2 or 1) names.  Print all three (or two or
      *  one), with asterisks by fields that don't match.
      *  If all are at EOF, we're through.
           IF INNAME1 = HIGH-VALUES GO TO EOJ.
           MOVE SPACES TO DETAIL-LINE.
           MOVE INNAME1 TO DET-NAME.
           MOVE INDATE1 TO DET-DATE1.
           MOVE INSIZE1 TO DET-SIZE1.
           MOVE INEPA1 TO DET-EPA1.
           MOVE INALIAS1 TO DET-ALIAS1.
           IF FILECOUNT = '1'
               PERFORM WRITE-DETAIL THRU WRITE-DETAIL-EXIT
               GO TO READ-ALL-3.
           MOVE INDATE2 TO DET-DATE2.
           MOVE INSIZE2 TO DET-SIZE2.
           MOVE INEPA2 TO DET-EPA2.
           MOVE INALIAS2 TO DET-ALIAS2.
           IF FILECOUNT = '3' GO TO COMPARE-3-DATES.
      *  We're comparing only 2 files, and the names match.
           IF INDATE1 NOT = INDATE2
               MOVE '*' TO DET-NAME-ASTER
               MOVE '*' TO DET-DATE1-ASTER
               MOVE '*' TO DET-DATE2-ASTER.
           IF INSIZE1 NOT = INSIZE2
               MOVE '*' TO DET-NAME-ASTER
               MOVE '*' TO DET-SIZE1-ASTER
               MOVE '*' TO DET-SIZE2-ASTER.
           IF INEPA1 NOT = INEPA2
               MOVE '*' TO DET-NAME-ASTER
               MOVE '*' TO DET-EPA1-ASTER
               MOVE '*' TO DET-EPA2-ASTER.
           IF INALIAS1 NOT = INALIAS2
               MOVE '*' TO DET-NAME-ASTER
               MOVE '*' TO DET-ALIAS1-ASTER
               MOVE '*' TO DET-ALIAS2-ASTER.
           GO TO PRINT-ALL-3.
       COMPARE-3-DATES.
           MOVE INDATE3 TO DET-DATE3.
           MOVE INSIZE3 TO DET-SIZE3.
           MOVE INEPA3 TO DET-EPA3.
           MOVE INALIAS3 TO DET-ALIAS3.
           IF INDATE1 = INDATE2 AND INDATE1 = INDATE3
               GO TO COMPARE-3-SIZES.
           MOVE '*' TO DET-NAME-ASTER.
      *      If 2 of the 3 dates match, put asterisk only by the one
      *      that doesn't.
           IF INDATE1 = INDATE2
               MOVE '*' TO DET-DATE3-ASTER
               GO TO COMPARE-3-SIZES.
           IF INDATE1 = INDATE3
               MOVE '*' TO DET-DATE2-ASTER
               GO TO COMPARE-3-SIZES.
           IF INDATE2 = INDATE3
               MOVE '*' TO DET-DATE1-ASTER
               GO TO COMPARE-3-SIZES.
      *      All 3 sizes are different: put asterisks by all three.
           MOVE '*' TO DET-DATE1-ASTER.
           MOVE '*' TO DET-DATE2-ASTER.
           MOVE '*' TO DET-DATE3-ASTER.
       COMPARE-3-SIZES.
           IF INSIZE1 = INSIZE2 AND INSIZE1 = INSIZE3
               GO TO COMPARE-3-EPAS.
           MOVE '*' TO DET-NAME-ASTER.
           IF INSIZE1 = INSIZE2
               MOVE '*' TO DET-SIZE3-ASTER
               GO TO COMPARE-3-EPAS.
           IF INSIZE1 = INSIZE3
               MOVE '*' TO DET-SIZE2-ASTER
               GO TO COMPARE-3-EPAS.
           IF INSIZE2 = INSIZE3
               MOVE '*' TO DET-SIZE1-ASTER
               GO TO COMPARE-3-EPAS.
           MOVE '*' TO DET-SIZE1-ASTER.
           MOVE '*' TO DET-SIZE2-ASTER.
           MOVE '*' TO DET-SIZE3-ASTER.
       COMPARE-3-EPAS.
           IF INEPA1 = INEPA2 AND INEPA1 = INEPA3
               GO TO COMPARE-3-ALIASES.
           MOVE '*' TO DET-NAME-ASTER.
           IF INEPA1 = INEPA2
               MOVE '*' TO DET-EPA3-ASTER
               GO TO COMPARE-3-ALIASES.
           IF INEPA1 = INEPA3
               MOVE '*' TO DET-EPA2-ASTER
               GO TO COMPARE-3-ALIASES.
           IF INEPA2 = INEPA3
               MOVE '*' TO DET-EPA1-ASTER
               GO TO COMPARE-3-ALIASES.
           MOVE '*' TO DET-EPA1-ASTER.
           MOVE '*' TO DET-EPA2-ASTER.
           MOVE '*' TO DET-EPA3-ASTER.
       COMPARE-3-ALIASES.
           IF INALIAS1 = INALIAS2 AND INALIAS1 = INALIAS3
               GO TO PRINT-ALL-3.
           MOVE '*' TO DET-NAME-ASTER.
           IF INALIAS1 = INALIAS2
               MOVE '*' TO DET-ALIAS3-ASTER
               GO TO PRINT-ALL-3.
           IF INALIAS1 = INALIAS3
               MOVE '*' TO DET-ALIAS2-ASTER
               GO TO PRINT-ALL-3.
           IF INALIAS2 = INALIAS3
               MOVE '*' TO DET-ALIAS1-ASTER
               GO TO PRINT-ALL-3.
           MOVE '*' TO DET-ALIAS1-ASTER.
           MOVE '*' TO DET-ALIAS2-ASTER.
           MOVE '*' TO DET-ALIAS3-ASTER.
       PRINT-ALL-3.
      *  Match on all 3 (or all 2) names.  If all data fields also
      *  match, print only if LIST=ALL or LIST=DUPS was specified.
      *  If one or more data fields doesn't match, print in any
      *  case.
           IF PARM-OPTION = 'E' AND DET-NAME-ASTER NOT = '*'
               GO TO READ-ALL-3.
           PERFORM WRITE-DETAIL THRU WRITE-DETAIL-EXIT.
       READ-ALL-3.
           PERFORM READ-FILE1 THRU READ-FILE1-EXIT.
           IF FILECOUNT = '1' GO TO COMPARE.
           PERFORM READ-FILE2 THRU READ-FILE2-EXIT.
           IF FILECOUNT = '2' GO TO COMPARE.
           PERFORM READ-FILE3 THRU READ-FILE3-EXIT.
           GO TO COMPARE.
       NAME1-LOWER-THAN-NAME2.
           IF FILECOUNT = '2' GO TO PRINT-NAME1.
           IF INNAME1 < INNAME3
      *      1 < 2 and 1 < 3
               GO TO PRINT-NAME1.
           IF INNAME1 > INNAME3
      *      3 < 1 < 2
               GO TO PRINT-NAME3.
      *      (1=3) < 2
           GO TO PRINT-NAME1-AND-NAME3.
       NAME2-LOWER-THAN-NAME1.
           IF FILECOUNT = '2' GO TO PRINT-NAME2.
           IF INNAME2 < INNAME3
      *      2 < 1 AND 2 < 3
               GO TO PRINT-NAME2.
           IF INNAME2 > INNAME3
      *      3 < 2 < 1
               GO TO PRINT-NAME3.
      *      (2=3) < 1
           GO TO PRINT-NAME2-AND-NAME3.
       PRINT-NAME1.
      *  NAME1 has no match in FILE2 (if 3 files, in FILE3 either).
      *  Print it unless LIST=DUPS was specified.
           IF PARM-OPTION = 'D' GO TO NAME1-PRINTED.
           MOVE SPACES TO DETAIL-LINE.
           MOVE '*' TO DET-NAME-ASTER.
           MOVE INNAME1 TO DET-NAME.
           MOVE '*' TO DET-DATE1-ASTER.
           MOVE INDATE1 TO DET-DATE1.
           MOVE INSIZE1 TO DET-SIZE1.
           MOVE INEPA1 TO DET-EPA1.
           MOVE INALIAS1 TO DET-ALIAS1.
           MOVE '*' TO DET-DATE2-ASTER.
           MOVE '--------' TO DET-DATE2.
           IF FILECOUNT = '3' MOVE '*' TO DET-DATE3-ASTER,
               MOVE '--------' TO DET-DATE3.
           PERFORM WRITE-DETAIL THRU WRITE-DETAIL-EXIT.
       NAME1-PRINTED.
           PERFORM READ-FILE1 THRU READ-FILE1-EXIT.
           GO TO COMPARE.
       PRINT-NAME2.
      *  NAME2 has no match in FILE1 (if 3 files, in FILE3 either).
      *  Print it unless LIST=DUPS was specified.
           IF PARM-OPTION = 'D' GO TO NAME2-PRINTED.
           MOVE SPACES TO DETAIL-LINE.
           MOVE '*' TO DET-NAME-ASTER.
           MOVE INNAME2 TO DET-NAME.
           MOVE '*' TO DET-DATE1-ASTER.
           MOVE '--------' TO DET-DATE1.
           MOVE '*' TO DET-DATE2-ASTER.
           MOVE INDATE2 TO DET-DATE2.
           MOVE INSIZE2 TO DET-SIZE2.
           MOVE INEPA2 TO DET-EPA2.
           MOVE INALIAS2 TO DET-ALIAS2.
           IF FILECOUNT = '3' MOVE '*' TO DET-DATE3-ASTER,
               MOVE '--------' TO DET-DATE3.
           PERFORM WRITE-DETAIL THRU WRITE-DETAIL-EXIT.
       NAME2-PRINTED.
           PERFORM READ-FILE2 THRU READ-FILE2-EXIT.
           GO TO COMPARE.
       PRINT-NAME3.
      *  NAME3 has no match in either FILE1 or FILE2.
      *  Print it unless LIST=DUPS was specified.
           IF PARM-OPTION = 'D' GO TO NAME3-PRINTED.
           MOVE SPACES TO DETAIL-LINE.
           MOVE '*' TO DET-NAME-ASTER.
           MOVE INNAME3 TO DET-NAME.
           MOVE '*' TO DET-DATE1-ASTER.
           MOVE '--------' TO DET-DATE1.
           MOVE '*' TO DET-DATE2-ASTER.
           MOVE '--------' TO DET-DATE2.
           MOVE '*' TO DET-DATE3-ASTER.
           MOVE INDATE3 TO DET-DATE3.
           MOVE INSIZE3 TO DET-SIZE3.
           MOVE INEPA3 TO DET-EPA3.
           MOVE INALIAS3 TO DET-ALIAS3.
           PERFORM WRITE-DETAIL THRU WRITE-DETAIL-EXIT.
       NAME3-PRINTED.
           PERFORM READ-FILE3 THRU READ-FILE3-EXIT.
           GO TO COMPARE.
       PRINT-NAME1-AND-NAME2.
      *  FILES=3 was specified, and NAME1 and NAME2 match (whether or
      *  not their dates and other data do) but there is no matching
      *  member in FILE3.  Print in any case.
           MOVE SPACES TO DETAIL-LINE.
           MOVE '*' TO DET-NAME-ASTER.
           MOVE INNAME1 TO DET-NAME.
           IF INDATE1 NOT = INDATE2
               MOVE '*' TO DET-DATE1-ASTER
               MOVE '*' TO DET-DATE2-ASTER.
           MOVE INDATE1 TO DET-DATE1.
           IF INSIZE1 NOT = INSIZE2
               MOVE '*' TO DET-SIZE1-ASTER
               MOVE '*' TO DET-SIZE2-ASTER.
           MOVE INSIZE1 TO DET-SIZE1.
           IF INEPA1 NOT = INEPA2
               MOVE '*' TO DET-EPA1-ASTER
               MOVE '*' TO DET-EPA2-ASTER.
           MOVE INEPA1 TO DET-EPA1.
           IF INALIAS1 NOT = INALIAS2
               MOVE '*' TO DET-ALIAS1-ASTER
               MOVE '*' TO DET-ALIAS2-ASTER.
           MOVE INALIAS1 TO DET-ALIAS1.
           MOVE INDATE2 TO DET-DATE2.
           MOVE INSIZE2 TO DET-SIZE2.
           MOVE INEPA2 TO DET-EPA2.
           MOVE INALIAS2 TO DET-ALIAS2.
           MOVE '*' TO DET-DATE3-ASTER.
           MOVE '--------' TO DET-DATE3.
           PERFORM WRITE-DETAIL THRU WRITE-DETAIL-EXIT.
           PERFORM READ-FILE1 THRU READ-FILE1-EXIT.
           PERFORM READ-FILE2 THRU READ-FILE2-EXIT.
           GO TO COMPARE.
       PRINT-NAME1-AND-NAME3.
      *  FILES=3 was specified, and NAME1 and NAME3 match (whether or
      *  not their dates and other data do) but there is no matching
      *  member in FILE2.  Print in any case.
           MOVE SPACES TO DETAIL-LINE.
           MOVE '*' TO DET-NAME-ASTER.
           MOVE INNAME1 TO DET-NAME.
           IF INDATE1 NOT = INDATE3
               MOVE '*' TO DET-DATE1-ASTER
               MOVE '*' TO DET-DATE3-ASTER.
           MOVE INDATE1 TO DET-DATE1.
           IF INSIZE1 NOT = INSIZE3
               MOVE '*' TO DET-SIZE1-ASTER
               MOVE '*' TO DET-SIZE3-ASTER.
           MOVE INSIZE1 TO DET-SIZE1.
           IF INEPA1 NOT = INEPA3
               MOVE '*' TO DET-EPA1-ASTER
               MOVE '*' TO DET-EPA3-ASTER.
           MOVE INEPA1 TO DET-EPA1.
           IF INALIAS1 NOT = INALIAS3
               MOVE '*' TO DET-ALIAS1-ASTER
               MOVE '*' TO DET-ALIAS3-ASTER.
           MOVE INALIAS1 TO DET-ALIAS1.
           MOVE '*' TO DET-DATE2-ASTER.
           MOVE '--------' TO DET-DATE2.
           MOVE INDATE3 TO DET-DATE3.
           MOVE INSIZE3 TO DET-SIZE3.
           MOVE INEPA3 TO DET-EPA3.
           MOVE INALIAS3 TO DET-ALIAS3.
           PERFORM WRITE-DETAIL THRU WRITE-DETAIL-EXIT.
           PERFORM READ-FILE1 THRU READ-FILE1-EXIT.
           PERFORM READ-FILE3 THRU READ-FILE3-EXIT.
           GO TO COMPARE.
       PRINT-NAME2-AND-NAME3.
      *  FILES=3 was specified, and NAME2 and NAME3 match (whether or
      *  not their dates and other data do) but there is no matching
      *  member in FILE1.  Print in any case.
           MOVE SPACES TO DETAIL-LINE.
           MOVE '*' TO DET-NAME-ASTER.
           MOVE INNAME2 TO DET-NAME.
           MOVE '*' TO DET-DATE1-ASTER.
           MOVE '--------' TO DET-DATE1.
           IF INDATE2 NOT = INDATE3
               MOVE '*' TO DET-DATE2-ASTER
               MOVE '*' TO DET-DATE3-ASTER.
           MOVE INDATE2 TO DET-DATE2.
           IF INSIZE2 NOT = INSIZE3
               MOVE '*' TO DET-SIZE2-ASTER
               MOVE '*' TO DET-SIZE3-ASTER.
           MOVE INSIZE2 TO DET-SIZE2.
           IF INEPA2 NOT = INEPA3
               MOVE '*' TO DET-EPA2-ASTER
               MOVE '*' TO DET-EPA3-ASTER.
           MOVE INEPA2 TO DET-EPA2.
           IF INALIAS2 NOT = INALIAS3
               MOVE '*' TO DET-ALIAS2-ASTER
               MOVE '*' TO DET-ALIAS3-ASTER.
           MOVE INALIAS2 TO DET-ALIAS2.
           MOVE INDATE3 TO DET-DATE3.
           MOVE INSIZE3 TO DET-SIZE3.
           MOVE INEPA3 TO DET-EPA3.
           MOVE INALIAS3 TO DET-ALIAS3.
           PERFORM WRITE-DETAIL THRU WRITE-DETAIL-EXIT.
           PERFORM READ-FILE2 THRU READ-FILE2-EXIT.
           PERFORM READ-FILE3 THRU READ-FILE3-EXIT.
           GO TO COMPARE.
       READ-FILE1.
           CALL 'LODCMPIO' USING FILE-1, READ-ACTION, I-O-AREA-1.
       READ-FILE1-EXIT. EXIT.
       READ-FILE2.
           CALL 'LODCMPIO' USING FILE-2, READ-ACTION, I-O-AREA-2.
       READ-FILE2-EXIT. EXIT.
       READ-FILE3.
           CALL 'LODCMPIO' USING FILE-3, READ-ACTION, I-O-AREA-3.
       READ-FILE3-EXIT. EXIT.
       WRITE-DETAIL.
           IF LINE-CTR > 56
               ADD 1 TO PAGE-CTR
               MOVE PAGE-CTR TO HD1-PAGENO
               WRITE PRT-REC FROM HEADING-1
               WRITE PRT-REC FROM HEADING-2
               WRITE PRT-REC FROM HEADING-3
               WRITE PRT-REC FROM HEADING-4
               MOVE SPACES TO PRT-REC
               WRITE PRT-REC
               MOVE +7 TO LINE-CTR.
           MOVE DETAIL-LINE TO PRT-REC.
           WRITE PRT-REC.
           ADD 1 TO LINE-CTR.
           MOVE 'Y' TO HIT-SWITCH.
       WRITE-DETAIL-EXIT. EXIT.
       BAD-PARM.
           MOVE +1 TO HD1-PAGENO.
           WRITE PRT-REC FROM HEADING-1.
           WRITE PRT-REC FROM BADPARM-LINE.
           MOVE 16 TO RETURN-CODE.
           GO TO CLOSE-OUTPUT.
       EOJ.
           IF HIT-SWITCH = 'N' AND PARM-OPTION NOT = 'A'
               MOVE +1 TO HD1-PAGENO
               WRITE PRT-REC FROM HEADING-1
               WRITE PRT-REC FROM HEADING-2
               WRITE PRT-REC FROM HEADING-3
               MOVE ALL-MATCH-LINE TO PRT-REC
               WRITE PRT-REC.
           CALL 'LODCMPIO' USING FILE-1, CLOSE-ACTION.
           IF FILECOUNT = '1' GO TO CLOSE-OUTPUT.
           CALL 'LODCMPIO' USING FILE-2, CLOSE-ACTION.
           IF FILECOUNT = '2' GO TO CLOSE-OUTPUT.
           CALL 'LODCMPIO' USING FILE-3, CLOSE-ACTION.
       CLOSE-OUTPUT.
           CLOSE PRTFILE.
           GOBACK.
